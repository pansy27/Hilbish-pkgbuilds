# Maintainer: Pansy27 <uselessshogun99 at gmail dot com>

pkgname='hilbish-git'
pkgver='2.0.0.rc1.r23.g1024f93'
pkgrel='1'
pkgdesc="A comfy and nice little shell for Lua fans!"
arch=('x86_64')
url="https://github.com/rosettea/hilbish"
license=('MIT')
makedepends=('git' 'go>=1.17.0')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" "$pkgname")
options=('!emptydirs')
install='hilbish-git.install'
source=("$pkgname::git+https://github.com/Rosettea/hilbish.git"
		"git+https://github.com/Rosettea/Lunacolors.git"
		"git+https://github.com/Rosettea/Succulent.git"
		"git+https://github.com/kikito/inspect.lua.git"
		)
md5sums=('SKIP'
				'SKIP'
				'SKIP'
				'SKIP'
)

pkgver() {
	cd "$srcdir/$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/$pkgname"
	git submodule init
	git config submodule.externals/vendor/https://github.com/Rosettea/Lunacolors.git "$srcdir/Lunacolors"
	git config submodule.externals/vendor/https://github.com/Rosettea/Succulent.git "$srcdir/Succulent"
	git config submodule.externals/vendor/https://github.com/kikito/inspect.lua.git "$srcdir/inspect.lua"
  git -c protocol.file.allow=always submodule update
}

build() { 
	cd "$srcdir/$pkgname"
	go get -d ./...
	CGO_ENABLED=0 go build -ldflags "-s -w -X main.gitCommit=$(git rev-parse --short HEAD) -X main.gitBranch=$(git rev-parse --abbrev-ref HEAD)"
}

package() { 
	cd "$srcdir/$pkgname"
	install -Dm 0755 -v ./hilbish "$pkgdir/usr/bin/hilbish"
	mkdir -p "$pkgdir/usr/share/hilbish"
	cp -r docs emmyLuaDocs libs nature .hilbishrc.lua "$pkgdir/usr/share/hilbish"
	install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
